#!/usr/bin/perl

use strict;
use warnings;

#The computer system you're breaking into uses a weird scrambling function to store its passwords. It shouldn't be much trouble to create your own scrambled password so you can add it to the system; you just have to implement the scrambler.
#
#The scrambling function is a series of operations (the exact list is provided in your puzzle input). Starting with the password to be scrambled, apply each operation in succession to the string. The individual operations behave as follows:
#
#swap position X with position Y means that the letters at indexes X and Y (counting from 0) should be swapped.
#swap letter X with letter Y means that the letters X and Y should be swapped (regardless of where they appear in the string).
#rotate left/right X steps means that the whole string should be rotated; for example, one right rotation would turn abcd into dabc.
#rotate based on position of letter X means that the whole string should be rotated to the right based on the index of letter X (counting from 0) as determined before this instruction does any rotations. Once the index is determined, rotate the string to the right one time, plus a number of times equal to that index, plus one additional time if the index was at least 4.
#reverse positions X through Y means that the span of letters at indexes X through Y (including the letters at X and Y) should be reversed in order.
#move position X to position Y means that the letter which is at index X should be removed from the string, then inserted such that it ends up at index Y.
#For example, suppose you start with abcde and perform the following operations:
#
#swap position 4 with position 0 swaps the first and last letters, producing the input for the next step, ebcda.
#swap letter d with letter b swaps the positions of d and b: edcba.
#reverse positions 0 through 4 causes the entire string to be reversed, producing abcde.
#rotate left 1 step shifts all letters left one position, causing the first letter to wrap to the end of the string: bcdea.
#move position 1 to position 4 removes the letter at position 1 (c), then inserts it at position 4 (the end of the string): bdeac.
#move position 3 to position 0 removes the letter at position 3 (a), then inserts it at position 0 (the front of the string): abdec.
#rotate based on position of letter b finds the index of letter b (1), then rotates the string right once plus a number of times equal to that index (2): ecabd.
#rotate based on position of letter d finds the index of letter d (4), then rotates the string right once, plus a number of times equal to that index, plus an additional time because the index was at least 4, for a total of 6 right rotations: decab.
#After these steps, the resulting scrambled password is decab.
#
#Now, you just need to generate a new scrambled password and you can access the system. Given the list of scrambling operations in your puzzle input, what is the result of scrambling abcdefgh?
#
#--- Part Two ---
#
#You scrambled the password correctly, but you discover that you can't actually modify the password file on the system. You'll need to un-scramble one of the existing passwords by reversing the scrambling process.
#
#What is the un-scrambled version of the scrambled password fbgdceah?

my $input = [split //, "fbgdceah"];

my %lookup = ( 1, 7,
               3, 6,
               5, 5,
               7, 4,
               2, 2,
               4, 1,
               6, 0,
               0, 7 );


for my $inst (reverse <DATA>) {
    chomp $inst;
    if ( my ($swapposfrom, $swapposto) = $inst =~ /^swap position (\d+) with position (\d+)$/ ) {
        my $tmp = $input->[$swapposfrom];
        $input->[$swapposfrom] = $input->[$swapposto];
        $input->[$swapposto] = $tmp;
    } elsif ( my ($swapletfrom, $swapletto) = $inst =~ /^swap letter ([a-z]) with letter ([a-z])$/ ) {
        my ($fromloc, $toloc);
        for (my $i = 0; $i < @$input; $i++) {
            $fromloc = $i if $input->[$i] eq $swapletfrom;
            $toloc = $i if $input->[$i] eq $swapletto;
        }
        die "letters not found\n" unless defined $fromloc && defined $toloc;
        my $tmp = $input->[$fromloc];
        $input->[$fromloc] = $input->[$toloc];
        $input->[$toloc] = $tmp;
    } elsif ( my ($rotdir, $rotammount) = $inst =~ /^rotate (left|right) (\d+) steps?$/ ) {
        for (1..$rotammount) {
            if ($rotdir eq "left") {
                unshift @$input, (pop @$input);
            } else {
                push @$input, (shift @$input);
            }
        }
    } elsif ( my ($rotlet) = $inst =~ /^rotate based on position of letter ([a-z])$/ ) {
        my $i = 0;
        for (; $i < @$input; $i++) {
            last if $input->[$i] eq $rotlet;
        }
        my $j = $lookup{$i};
        for ( $i = 0; $i < $j; $i++ ) {
           unshift @$input, (pop @$input);
        }
    } elsif ( my ($revposfrom, $revposto) = $inst =~ /^reverse positions (\d+) through (\d+)$/ ) {
        splice @$input, $revposfrom, ($revposto - $revposfrom + 1), reverse @$input[$revposfrom..$revposto];
    } elsif ( my ($movefrom, $moveto) = $inst =~ /^move position (\d+) to position (\d+)$/ ) {
        splice @$input, $movefrom, 0, splice( @$input, $moveto, 1 );
    } else {
        die "Unknown instruction: $inst\n";
    }
}

print join "", @$input;
print "\n";

__DATA__
rotate right 1 step
swap position 2 with position 4
rotate based on position of letter g
rotate left 4 steps
swap position 6 with position 0
swap letter h with letter a
swap letter d with letter c
reverse positions 2 through 4
swap position 2 with position 4
swap letter d with letter e
reverse positions 1 through 5
swap letter b with letter a
rotate right 0 steps
swap position 7 with position 3
move position 2 to position 1
reverse positions 2 through 5
reverse positions 4 through 7
reverse positions 2 through 7
swap letter e with letter c
swap position 1 with position 7
swap position 5 with position 7
move position 3 to position 6
swap position 7 with position 2
move position 0 to position 7
swap position 3 with position 7
reverse positions 3 through 6
move position 0 to position 5
swap letter h with letter c
reverse positions 2 through 3
swap position 2 with position 3
move position 4 to position 0
rotate based on position of letter g
rotate based on position of letter g
reverse positions 0 through 2
swap letter e with letter d
reverse positions 2 through 5
swap position 6 with position 0
swap letter a with letter g
swap position 2 with position 5
reverse positions 2 through 3
swap letter b with letter d
reverse positions 3 through 7
swap position 2 with position 5
swap letter d with letter b
reverse positions 0 through 3
swap letter e with letter g
rotate based on position of letter h
move position 4 to position 3
reverse positions 0 through 6
swap position 4 with position 1
swap position 6 with position 4
move position 7 to position 5
swap position 6 with position 4
reverse positions 5 through 6
move position 0 to position 6
swap position 5 with position 0
reverse positions 2 through 5
rotate right 0 steps
swap position 7 with position 0
swap position 0 with position 2
swap position 2 with position 5
swap letter h with letter c
rotate left 1 step
reverse positions 6 through 7
swap letter g with letter a
reverse positions 3 through 7
move position 2 to position 4
reverse positions 0 through 6
rotate based on position of letter g
swap position 0 with position 6
move position 2 to position 0
rotate left 3 steps
reverse positions 2 through 5
rotate based on position of letter a
reverse positions 1 through 4
move position 2 to position 3
rotate right 2 steps
rotate based on position of letter f
rotate based on position of letter f
swap letter g with letter a
rotate right 0 steps
swap letter f with letter h
swap letter f with letter b
swap letter d with letter e
swap position 0 with position 7
move position 3 to position 0
swap position 3 with position 0
rotate right 4 steps
rotate based on position of letter a
reverse positions 0 through 7
rotate left 6 steps
swap letter d with letter h
reverse positions 0 through 4
rotate based on position of letter f
move position 5 to position 3
move position 1 to position 3
move position 6 to position 0
swap letter f with letter c
rotate based on position of letter h
reverse positions 6 through 7
